// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "./DevSwap.sol";

contract DevSwapFactory {
    mapping(address => mapping(address => address)) public getPair;
    address[] public allPairs;

    event PairCreated(address indexed token0, address indexed token1, address pair, uint256 pairIndex);

    function createPair(address tokenA,address tokenB) external returns (address pair) {

        require(tokenA != address(0), "Gecersiz tokenA adresi");
        require(tokenB != address(0), "Gecersiz tokenB adresi");
        require(tokenA != tokenB, "Tokenlar ayni olamaz");

        (address token0, address token1) = tokenA < tokenB
            ? (tokenA, tokenB)
            : (tokenB, tokenA);

        require(getPair[token0][token1] == address(0),"Pair zaten mevcut");

        pair = address(new DevSwap(token0, token1));

        getPair[token0][token1] = pair;
        getPair[token1][token0] = pair;

        allPairs.push(pair);

        emit PairCreated(
            token0,
            token1,
            pair,
            allPairs.length - 1
        );
    }

    function allPairsLength() external view returns (uint256) {
        return allPairs.length;
    }
}

